;OEM1_0 PA Vocal reverb

;pot0 = reverb decay time
;pot1 = predelay (0-100mS at Fs=46KHz)
;pot2 = damping, increases with clockwise rotation

;This program is especially designed for vocals in high gain PA applications.
;It has been developed to have maximum impact, but a flat response so that 
;it can be heavily mixed into live PA applications without feedback.

;The impulse response is rough, for a more eventful decay, but eventually 
;develops high diffusion.

;The frequency response is very flat, to reduce any tendancy toward feedback
;in high gain situations.

;the algorithm incorporates variable predelay to give better clarity to the 
;initial sound.

;Vibrato is added within the reverb delay elements to smooth the decay 
;and further remove any tendancy toward feedback.

;Input is expected to be mono, from single effects send, output is wide stereo.

mem	pdel	4600
mem	iap1	156
mem	iap2	223
mem	iap3	332
mem	iap4	448
;
mem	ap1	1251
mem	ap1b	1751
mem	ap2	1443
mem	ap2b	1343
mem	ap3	1582
mem	ap3b	1981
mem	ap4	1274
mem	ap4b	1382
;
mem	del1	3559
mem	del2	2945
mem	del3	3976
mem	del4	4445

equ 	temp	reg0
equ	hpf1	reg1
equ	hpf2	reg2
equ	hpf3	reg3
equ	hpf4	reg4
equ	lpf1	reg5
equ	lpf2	reg6
equ	lpf3	reg7
equ	lpf4	reg8
equ	rt	reg9
equ	iapout	reg10
equ	pdelo	reg11

;constants:

equ	kfh	0.01
equ	kfl	0.4
equ	kiap	0.5
equ	klap	0.6

;prepare decay time pot:

rdax	pot0,1
sof	0.6,0.3
wrax	rt,0			;rt ranges 0.3 to 0.9

;do variable predelay:

skp	run,1
wldr	rmp0,0,4096		;initialize predelay

rdax	adcl,0.5		;put inputs into predelay
rdax	adcr,0.5
wra	pdel,0

cho	rda,rmp0,reg|compc,pdel	;get outputs from predelay, interpolated
cho	rda,rmp0,0,pdel+1
wrax	pdelo,0			;write predelay output to register

cho	rdal,rmp0		;read current predelay pointer
rdax	pot1,-0.5		;subtract pot for servo control of pointer
wrax	rmp0_rate,0		;maintain predelay pointer

;now run predelayed signal into 4 all passes:

rdax	pdelo,0.25		;attenuate signal to avoid clipping	
rda	iap1#,kiap	
wrap	iap1,-kiap		
rda	iap2#,kiap	
wrap	iap2,-kiap		
rda	iap3#,kiap	
wrap	iap3,-kiap		
rda	iap4#,kiap	
wrap	iap4,-kiap		
wrax	iapout,0		;write to register for ring injection

;now do reverb ring, use temp as reg for filtering:

;aps into delay1:

rda	del4#,1		;read previous delay	
mulx	rt		;multiply by reverb time coefficient
rdax	iapout,1	;read left input from input allpass filter bank
rda	ap1#,klap	;do an allpass filter
wrap	ap1,-klap
rda	ap1b#,klap	;do second all pass filter
wrap	ap1b,-klap	
wrax	temp,1		;write to temp, keep in acc
rdfx	lpf1,kfl	;low pass filter
wrlx	lpf1,-1
rdfx	hpf1,kfh	;high pass filter
wrhx	hpf1,-1
rdax	temp,-1
mulx	pot2		;crossfade between filter and no filter
rdax	temp,1
wra	del1,0		;write to next delay, clear accumulator

;aps into delay2:

rda	del1#,1		
mulx	rt
rdax	iapout,1
rda	ap2#,klap	
wrap	ap2,-klap		
rda	ap2b#,klap	
wrap	ap2b,-klap		
wrax	temp,1		
rdfx	lpf2,kfl
wrlx	lpf2,-1
rdfx	hpf2,kfh
wrhx	hpf2,-1
rdax	temp,-1
mulx	pot2
rdax	temp,1
wra	del2,0		

;aps into delay3:

rda	del2#,1		
mulx	rt
rdax	iapout,1		
rda	ap3#,klap	
wrap	ap3,-klap		
rda	ap3b#,klap	
wrap	ap3b,-klap		
wrax	temp,1		
rdfx	lpf3,kfl
wrlx	lpf3,-1
rdfx	hpf3,kfh
wrhx	hpf3,-1
rdax	temp,-1
mulx	pot2
rdax	temp,1	
wra	del3,0

;aps into delay4:

rda	del3#,1.0		
mulx	rt
rdax	iapout,1
rda	ap4#,klap	
wrap	ap4,-klap		
rda	ap4b#,klap	
wrap	ap4b,-klap		
wrax	temp,1		
rdfx	lpf4,kfl
wrlx	lpf4,-1
rdfx	hpf4,kfh
wrhx	hpf4,-1
rdax	temp,-1
mulx	pot2
rdax	temp,1	
wra	del4,0		

;take outputs as taps from reverb ring:

rda	del1,1.9	;recover gain
wrax	dacl,0		;write output, clear accumulator
		
rda	del3,1.9
wrax	dacr,0	

;set up lfo, 1Hz to 2Hz, +/-100 samples, for smearing loop all passes:

skp	run,2
wlds	sin0,35,100
wlds	sin1,46,100

;smear 4 allpass filters in reverb ring:

cho 	rda,sin0,reg|compc,ap1+100	;sin0
cho	rda,sin0,0,ap1+101
wra	ap1+200,0

cho 	rda,sin0,cos|compc,ap2+100	;cos0
cho	rda,sin0,cos,ap2+101	
wra	ap2+200,0

cho 	rda,sin1,reg|compc,ap3+100	;sin1
cho	rda,sin1,0,ap3+101
wra	ap3+200,0

cho 	rda,sin1,cos|compc,ap4+100	;cos1
cho	rda,sin1,cos,ap4+101	
wra	ap4+200,0

